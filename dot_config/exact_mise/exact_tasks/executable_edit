#!/usr/bin/env nu
#MISE description="Edit various configuration files"
#MISE quiet=true
#MISE alias="e"

let files = {
  tmux: [tmux tmux.conf]
  wezterm: [wezterm wezterm.lua]
  nvim: [nvim]
  zsh: [zsh .zshrc]
  sketchybar: [sketchybar sketchybarrc]
  borders: [borders bordersrc]
  nu: [nushell config.nu]
  mise: [mise config.toml]
  aerospace: [aerospace aerospace.toml]
}

# Write shell completions to file
def "main completions" [shell: string] {
  const spec = path self | path dirname | path join "edit.kdl"
  usage g completion $shell edit -f $spec
  | save --force (match ($shell) {
    zsh => ([$env.XDG_CONFIG_HOME zsh functions _edit] | path join)
  } | tee { print -e $"(ansi cyan)INFO(ansi reset) ($shell) completions written to ($in)"})
}

# List the programs that can be edited
def "main list" [] {
  $files | columns | str join "\n"
}

# Edit various configuration files
def main [
  program?: string@"main _programs" = ''
] {
  let editor = if (which neovide | is-not-empty) {
    [neovide --fork]
  } else {
    $env.VISUAL? | default $env.EDITOR? | default "vim" | [$in]
  }
  $files
  | get -o $program
  | default []
  | if ($in | is-empty) { $env.MISE_TASK_FILE } else {
    $in | prepend $env.XDG_CONFIG_HOME | path join
  }
  | run-external ...$editor $in
}
