#!/usr/bin/env -S mise x aqua:nushell/nushell@0.106.1 -- nu
# vim: ft=nu

let info = ( spotify_player get key playback | from json)
let is_active = $info | get device?.is_active? | default false
let track = $info | get item?.name? | default ""
let artists = $info | get item?.artists?.name? | default [] | str join ", "

[
  --animate quadratic 30
  --set $env.NAME
] ++ (if ($is_active) {
  match [$artists $track] {
    [$a ""] if $a != "" => $a
    ["" $t] if $t != "" => $t
    [$a $t] => $"($a) - ($t)"
    _ => "No track info available..."
  }
  | [drawing=on $"label=($in)"]
} else {
  [drawing=off]
})
| run-external sketchybar ...$in
