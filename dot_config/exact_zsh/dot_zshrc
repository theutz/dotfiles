# ╭────────────────╮
# │ Pre-Completion │
# ╰────────────────╯

# Setup starship
if command -v starship &>/dev/null; then
  eval "$(starship init zsh)"
fi

# A better version of `run-help`
if alias run-help &>/dev/null; then
  unalias run-help && autoload -Uz run-help
fi

# Activate Mise
if command -v mise &>/dev/null; then
  eval "$($HOME/.local/bin/mise activate zsh)"
fi

# Setup yazi wrapper
if command -v yazi &>/dev/null; then
  autoload -Uz y
fi

# SSH Agent
if command -v ssh-agent &>/dev/null; then
  _ssh_agent_env="${_ssh_agent_env:-${XDG_CACHE_HOME}/ssh-agent.env}"
  _ssh_agent_sock="${_ssh_agent_sock:-${XDG_CACHE_HOME}/ssh-agent.sock}"

  if [[ ! -S "$SSH_AUTH_SOCK" ]]; then
    source "$_ssh_agent_env" 2>/dev/null
    if ! ps -U "$LOGNAME" -o pid,ucomm | grep -q -- "${SSH_AGENT_PID:--1} ssh-agent"; then
      mkdir -p "$_ssh_agent_env:h"
      eval "$(ssh-agent | sed '/^echo /d' | tee "$_ssh_agent_env")"
    fi
  fi

  if [[ -S "$SSH_AUTH_SOCK" && "$SSH_AUTH_SOCK" != "$_ssh_agent_sock" ]]; then
    mkdir -p "$_ssh_agent_sock:h"
    ln -sf "$SSH_AUTH_SOCK" "$_ssh_agent_sock"
    export SSH_AUTH_SOCK="$_ssh_agent_sock"
  fi

  if ssh-add -l 2>&1 | grep -q 'The agent has no identities'; then
    ssh-add ~/.ssh/id_ed25519 2>/dev/null
  fi

  # Clean up.
  unset _ssh_{dir,identities} _ssh_agent_{env,sock}
fi

# Aliases
alias ls=eza
alias cat=bat
alias cd=z
alias lg=lazygit
alias g=git
alias cm=chezmoi
alias m=mise

# ╭─────────────────╮
# │ Init Completion │
# ╰─────────────────╯

autoload -Uz compinit
compinit

# ╭─────────────────╮
# │ Post-Completion │
# ╰─────────────────╯

# Initialize carapace
export CARAPACE_BRIDGES='zsh,fish,bash,inshellisense'
zstyle ':completion:*' format $'\e[2;37mCompleting %d\e[m'
source <(carapace _carapace)

# Init zoxide
eval "$(zoxide init zsh)"

# Syntax Highlighting
source "$(mise where http:zsh-syntax-highlighting)/zsh-syntax-highlighting.zsh"
