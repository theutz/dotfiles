#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${XDG_CACHE_HOME:-$HOME/.cache}/prezto/zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    if command mkdir "${zcompdump}.zwc.lock" 2>/dev/null; then
      zcompile "$zcompdump"
      command rmdir  "${zcompdump}.zwc.lock" 2>/dev/null
    fi
  fi
} &!

# Execute code only if STDERR is bound to a TTY.
if [[ -o INTERACTIVE && -t 2 ]]; then
  if (( $+commands[neofetch] )); then
    neofetch
  fi

  if (( $+commands[chezmoi] )); then
    if [[ "$(chezmoi status --exclude scripts | wc -c)" -gt 1 ]]; then
      tput bold; tput setaf 1
      printf "Chezmoi has unapplied changes...\n\n"
      tput sgr0; tput op
      chezmoi status --exclude scripts
      print
    fi
  fi

  if [[ -n "$(git -C $ZPREZTODIR status --porcelain)" ]]; then
    tput bold; tput setaf 1
    printf "Prezto has uncommitted changes...\n\n"
    tput sgr0; tput op
    git -C $ZPREZTODIR status --short
    print
  fi

fi >&2
