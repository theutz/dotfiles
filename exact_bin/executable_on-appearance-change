#!/usr/bin/env nu

def change-zellij-theme [mode: string] {
  let theme = match ($mode) {
    light => "catppuccin-latte"
    dark => "catppuccin-mocha"
  }

  let file = $env.XDG_CONFIG_HOME | path join zellij config.kdl
  open $file
  | str replace --regex 'theme ".*?"' $'theme "($theme)"'
  | save --force $file
   
  $file
}

def change-starship-palette [mode: string]: nothing -> string {
  let theme = match ($mode) {
    light => "catppuccin_latte"
    dark  => "catppuccin_mocha"
    _ => "catppuccin_frappe"
  }

  let file = $env.XDG_CONFIG_HOME | path join starship.toml

  open $file | upsert palette $theme | save --force $file

  $file
}

def change-helix-theme [mode: string]: nothing -> string {
  let theme = match ($mode) {
    light => "catppuccin_latte"
    dark => "catppuccin_mocha"
  }

  let file = $env.XDG_CONFIG_HOME | path join helix config.toml

  open $file | upsert theme $theme | save --force $file
  pkill -USR1 hx # Reload helix config

  $file
}

def reload-sketchybar [] {
  sketchybar --reload
}

def main [mode: string] {
  reload-sketchybar
  [
    {|| change-helix-theme $mode }
    {|| change-starship-palette $mode }
    {|| change-zellij-theme $mode }
  ]
  | par-each {|fn| do $fn }
  | chezmoi re-add ...$in
}

