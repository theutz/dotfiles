#!/usr/bin/env just --unstable --working-directory . --justfile

set shell := ["zsh", "-cu"]
set quiet

cmd := justfile()
dir := join(env('XDG_CONFIG_HOME', join(env('HOME'), '.config')), "tmuxp")

[private]
default:
  @{{cmd}} --list

alias ls := list
# List all files
list:
  gum file {{dir}}

alias l := load
# Load a tmuxp session in detached state
load:
  {{cmd}} ls | xargs tmuxp load -d

alias a := attach
# Attach to a tmuxp session
attach:
  {{cmd}} ls | xargs tmuxp load -y

alias rm := delete
# Send a tmuxp definition to the trash
delete:
  #!/usr/bin/env zsh
  gum file {{dir}} | read file
  if
    gum confirm "Are you sure you want to send $file to the trash?"
  then
    trash $file &&
    gum log -l info "$file deleted."
  fi

alias mk := create
# Create a new tmuxp session
create session:
  #!/usr/bin/env zsh
  tmuxp ls |
    gum filter --select-if-one \
      --limit=1 \
      --header="Select a session to use as a template..." \
      --placeholder="session_name" |
    xargs -I{} cp {{dir}}/{}.yml {{join(dir, session)}}.yml

alias e := edit
# Edit a template in $EDITOR
edit:
  $EDITOR $({{cmd}} ls)
